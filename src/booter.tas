
;GEOS Kernal - booter code
;reassembled by Maciej 'YTM/Alliance' Witkowiak
;19,20-3-99;
;4.4 - custom expansions on-boot tests (not tested)
;5.4 - DeskTop ram loader
;20.4 - DB if DeskTop not found (no DT reboot - bug somewhere)

;17.8 - ACME port
;21.8.2000 - fixed directory position bug (bootTr2)
;22.9.2000 - macros, conditionale fixes

		SEI
		CLD
		LDX #$ff
		TXS
		+LoadB CPU_DATA, RAM_64K
		LDA #<_NMIHandler
		STA $fffa
		STA $fffc
		STA $fffe
		LDA #>_NMIHandler
		STA $fffb
		STA $fffd
		STA $ffff
		JSR ClrScr
		JSR i_FillRam
		!word $0500
		!word dirEntryBuf
		!byte 0
		JSR FirstInit
		JSR MOUSE_JMP
		+LoadB interleave, currentInterleave
		LDY curDevice
	!if (use1541) {
		LDA #DRV_1541
	} else {
	!if (use1571) {
		LDA #DRV_1571
	} else {
	!if (use1581) {
		LDA #DRV_1581
	}}}
		STA _driveType,y
		STY curDrive
	!if (REUPresent) {
		LDX #0
		LDA $5f0f
		CMP #$13
		BNE Boot2
		LDY #3
Boot1		LDA $5f06,y
		CMP bootTest,y
		BNE Boot2
		DEY
		BPL Boot1
	}
		LDX #$80
Boot2		TXA
		STA firstBoot
		BEQ Boot4
	!if (REUPresent) {
		LDY #2
Boot3		LDA $5f18,y
		STA year,y
		DEY
		BPL Boot3
		+MoveB $5f12, sysRAMFlg
		+bra Boot6
	}
Boot4		+LoadB CPU_DATA, IO_IN
		LDA cia1base+15
		AND #%01111111
		STA cia1base+15
		+LoadB cia1base+11, currentHour | (AMPM << 7)
		+LoadB cia1base+10, currentMinute
		+LoadW cia1base+8, 0
		+LoadB CPU_DATA, $30
		+LoadB year, currentYear
		+LoadB month, currentMonth
		+LoadB day, currentDay
		LDX #7
		LDA #$bb
Boot5		STA A8FE8,x
		DEX
		BPL Boot5
		+LoadB A8FF0, $bf
Boot6		LDA #>_IRQHandler
		STA $ffff
		LDA #<_IRQHandler
		STA $fffe
	!if (use2MHz) {
		+LoadB rasreg, $fc
	}
	!if (useRamCart64 | useRamCart128) {
		+LoadB CPU_DATA, IO_IN
		+LoadW RAMC_BASE, 0
		ldx RAMC_WINDOW
		ldy RAMC_WINDOW+$80
		lda #"M"
		sta RAMC_WINDOW
		lda #"W"
		sta RAMC_WINDOW+$80
		cmp RAMC_WINDOW+$80
		bne BootNotRC
		lda RAMC_WINDOW
		cmp #"M"
		bne BootNotRC
		stx RAMC_WINDOW
		sty RAMC_WINDOW
		jmp BootRC_OK
BootNotRC	+LoadB CPU_DATA, $30
		lda #>ExpFaultDB
		sta r0H
		lda #<ExpFaultDB
		sta r0L
		jsr DoDlgBox
		jmp ToBASIC
BootRC_OK	+LoadB CPU_DATA, $30
	}

	!if (usePlus60K) {
		+LoadB CPU_DATA, IO_IN
		ldx #0
BootP6K_1	lda Plus60KTest,x
		sta $0400,x
		inx
		cpx #Plus60KTestEnd-Plus60KTest
		bne BootP6K_1
		jsr $0400
		bne BootP6K_OK
		+LoadB CPU_DATA, $30
		lda #>ExpFaultDB
		sta r0H
		lda #<ExpFaultDB
		sta r0L
		jsr DoDlgBox
		jmp ToBASIC
BootP6K_OK	+LoadB CPU_DATA, $30
	}

BootCont	JSR OpenDisk
		JSR GetDirHead
		+bnex Boot7
		LDA #"K"
		CMP curDirHead+OFF_GS_DTYPE
		BEQ Boot7
		STA curDirHead+OFF_GS_DTYPE
		JSR PutDirHead
Boot7		+MoveB curDirHead+0, bootTr2
		+MoveB curDirHead+1, bootSec2
		+MoveB bootSec, r1H
		+MoveB bootTr, r1L
		+AddVB 32, bootOffs
		BNE Boot10
Boot8		+MoveB bootSec2, r1H
		+MoveB bootTr2, r1L
		BNE Boot10
		LDA NUMDRV
		BNE Boot9
		INC NUMDRV
Boot9		LDA #>_EnterDeskTop
		STA EnterDeskTop+2
		LDA #<_EnterDeskTop
		STA EnterDeskTop+1
	!if (useRamExp) {
		jsr LoadDeskTop
	}
		JMP _EnterDeskTop

Boot10		+MoveB r1H, bootSec
		+MoveB r1L, bootTr
		+LoadW r4, diskBlkBuf
		JSR GetBlock
		+bnex Boot9
		+MoveB diskBlkBuf+1, bootSec2
		+MoveB diskBlkBuf, bootTr2
Boot101 	LDY bootOffs
		LDA diskBlkBuf+2,y
		BEQ Boot11
		LDA diskBlkBuf+$18,y
		CMP #AUTO_EXEC
		BEQ Boot12
Boot11		+AddVB 32, bootOffs
		BNE Boot101
		BEQ Boot8
Boot12		LDX #0
Boot13		LDA diskBlkBuf+2,y
		STA dirEntryBuf,x
		INY
		INX
		CPX #30
		BNE Boot13
		+LoadW r9, dirEntryBuf
		+LoadB r0, 0
		LDA #>_BootEnterDeskTop
		STA EnterDeskTop+2
		LDA #<_BootEnterDeskTop
		STA EnterDeskTop+1
		LDA #>_EnterDeskTop-1
		PHA
		LDA #<_EnterDeskTop-1
		PHA
		LDA #>_FirstInit-1
		PHA
		LDA #<_FirstInit-1
		PHA
		JMP LdApplic

_BootEnterDeskTop
		SEI
		CLD
		LDX #$ff
		TXS
		JSR _DoFirstInitIO
		JSR InitGEOEnv
		JMP Boot8

	!if (usePlus60K) {
Plus60KTest
		ldx #0
		stx PLUS60K_CR
		ldy $1180
		+LoadB $1180, ("M")
		+LoadB PLUS60K_CR, $80
		lda $1180
		stx PLUS60K_CR
		sty $1180
		cmp #"M"
		rts
Plus60KTestEnd
	}

	!if (useRamExp) {
LoadDeskTop	+LoadB a0L, NULL
		+LoadB BVChainTab, 1	;1 - first free
LoadDTLp	lda #>DeskTopName
		sta r6H
		lda #<DeskTopName
		sta r6L
		jsr FindFile
		+beqx LoadDTCont
		lda #>LoadDT_DB
		sta r0H
		lda #<LoadDT_DB
		sta r0L
		jsr DoDlgBox
		jsr NewDisk
		jmp LoadDTLp
LoadDTCont	jsr GetFHdrInfo
		lda fileHeader+O_GHST_ADDR+1
		sta DeskTopStart+1
		lda fileHeader+O_GHST_ADDR
		sta DeskTopStart
		lda fileHeader+O_GHST_VEC+1
		sta DeskTopExec+1
		lda fileHeader+O_GHST_VEC
		sta DeskTopExec

		+LoadB DeskTopOpen, $88
		lda #>DeskTopName
		sta r0H
		lda #<DeskTopName
		sta r0L
		jsr OpenRecordFile

BLoadDTop	lda a0L
		jsr PointRecord
		+bnex BVLast
		+LoadW r2, $ffff
		+LoadW r7, BVBuff
		jsr ReadRecord

		lda r7H
		+subv (>BVBuff)
		tay
		ldx a0L
		bne BLoadDTop_1
		sty DeskTopLgh
BLoadDTop_1	clc
		adc BVChainTab,x
		adc #1
		sta BVChainTab+1,x
		inc a0L

		+LoadB r1H, 0
		lda BVChainTab,x
		sta r1L
		+LoadW r0, BVBuff
		sty r2H
		jsr RamExpWrite

		+LoadB CPU_DATA, IO_IN
		inc $d020
		+LoadB CPU_DATA, $30
		jmp BLoadDTop

BVLast		jsr RamExpGetStat
		+MoveB a0L, diskBlkBuf+DTOP_CHNUM
		inc a0L
		ldx #0
BVLast_1	lda BVChainTab,x
		sta diskBlkBuf+DTOP_CHAIN,x
		inx
		cpx a0L
		bne BVLast_1
		sta diskBlkBuf+RAM_EXP_1STFREE
		+LoadB DeskTopOpen, 0
		jmp RamExpPutStat
	}

bootTr		!byte 0
bootSec 	!byte 0
bootTr2 	!byte DIR_TRACK
bootSec2	!byte 1
bootOffs	!byte $e0

bootTest	!text "GEOS"

	!if (useRamExp) {
LoadDT_DB
		!byte DEF_DB_POS | 1
		!byte DBTXTSTR, TXT_LN_X, TXT_LN_1_Y+6
		!word LoadDT_Str0
		!byte DBTXTSTR, TXT_LN_X, TXT_LN_2_Y+6
		!word LoadDT_Str1
		!byte OK, DBI_X_2, DBI_Y_2
		!byte NULL

LoadDT_Str0	!text BOLDON, "Please insert a disk", NULL
LoadDT_Str1	!text "with deskTop V1.5 or higher", NULL

ExpFaultDB	!byte DEF_DB_POS | 1
		!byte DBTXTSTR, TXT_LN_X, TXT_LN_1_Y
		!word ExpFaultStr
		!byte DBTXTSTR, TXT_LN_X, TXT_LN_2_Y
		!word ExpFaultStr2
		!byte OK, DBI_X_1, DBI_Y_2
		!byte NULL

ExpFaultStr	!byte BOLDON
		!text "This version of GEOS works", NULL
ExpFaultStr2	
	!if (useRamCart64 | useRamCart128) {
		!text "only with RamCart expansion.", NULL
	}
	!if (usePlus60K) {
		!text "only with +60K expansion.", NULL
	}

	}

BVChainTab
